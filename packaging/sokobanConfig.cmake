cmake_minimum_required(VERSION 3.19)

set(sokoban_known_comps static shared)
set(sokoban_comp_static NO)
set(sokoban_comp_shared NO)
foreach (sokoban_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (sokoban_comp IN_LIST sokoban_known_comps)
        set(sokoban_comp_${sokoban_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "sokoban does not recognize component `${sokoban_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (sokoban_comp_static AND sokoban_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "sokoban `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(sokoban_static_targets "${CMAKE_CURRENT_LIST_DIR}/sokoban-static-targets.cmake")
set(sokoban_shared_targets "${CMAKE_CURRENT_LIST_DIR}/sokoban-shared-targets.cmake")

macro(sokoban_load_targets type)
    if (NOT EXISTS "${sokoban_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "sokoban `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${sokoban_${type}_targets}")
endmacro()

if (sokoban_comp_static)
    sokoban_load_targets(static)
elseif (sokoban_comp_shared)
    sokoban_load_targets(shared)
elseif (DEFINED sokoban_SHARED_LIBS AND sokoban_SHARED_LIBS)
    sokoban_load_targets(shared)
elseif (DEFINED sokoban_SHARED_LIBS AND NOT sokoban_SHARED_LIBS)
    sokoban_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${sokoban_shared_targets}")
        sokoban_load_targets(shared)
    else ()
        sokoban_load_targets(static)
    endif ()
else ()
    if (EXISTS "${sokoban_static_targets}")
        sokoban_load_targets(static)
    else ()
        sokoban_load_targets(shared)
    endif ()
endif ()
